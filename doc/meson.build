sphinx = find_program('sphinx-build')

if get_option('documentation')
  sphinx_output = custom_target(
    'HTML documentation',
    output: 'html',
    input: [
      'index.rst', 'user.rst', 'developer.rst',
      'plugins.rst',
      'protocol.rst',
      'conf.py',
    ],
    command: [sphinx, '-q', '-b', 'html', '-d', '@OUTDIR@/doctrees', meson.current_source_dir(), '@OUTPUT@'],
    build_by_default: true,
    install: true,
    install_dir: join_paths(get_option('datadir'), 'doc', meson.project_name()),
  )

  custom_target(
    'upload',
    input: sphinx_output,
    output: 'upload',
    build_always_stale: true,
    command: [
      'rsync', '-vpruz', '--delete', meson.current_build_dir() + '/',
      'www.musicpd.org:/var/www/mpd/doc/',
      '--chmod=Dug+rwx,Do+rx,Fug+rw,Fo+r',
      '--include=html', '--include=html/**',
      '--exclude=*',
    ],
  )
endif

if get_option('manpages')
  custom_target(
    'Manpage documentation',
    output: 'man',
    input: ['mpd.1.rst', 'mpd.conf.5.rst', 'conf.py'],
    command: [sphinx, '-q', '-b', 'man', '-d', '@OUTDIR@/man_doctrees', meson.current_source_dir(), '@OUTPUT@'],
    build_by_default: true,
    install: true,
    install_dir: get_option('mandir'),
  )
endif
